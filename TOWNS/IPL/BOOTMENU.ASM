INSTRUCTION_LOCATION		EQU		0506H	; (1,6)
FIRST_MENU_ITEM_LOCATION	EQU		0509H	; (5,9)
DISPLACED_DEMOSPLASh_MESSAGE_LOCATION	EQU		2A09H	; (42,9)
ERRORMESSAGE_LOCATION		EQU		0505H	; (5,5)
AUTOBOOT_MESSAGE_LOCATION	EQU		0508H

BOOTMENU_ITEMNUM_RETURN_TO_MAINMENU	EQU		0
BOOTMENU_ITEMNUM_INTERNAL_CD	EQU		1
BOOTMENU_ITEMNUM_FD0			EQU		2
BOOTMENU_ITEMNUM_SCSI_CD		EQU		4
BOOTMENU_ITEMNUM_SCSI_HDD_0		EQU		5
BOOTMENU_ITEMNUM_PARTITION_0	EQU		10


BOOTMENU				PROC

						CALL	CLEAR_FIVE_BELOW

						TEST	CS:[CONTROL_FLAGS],CONTROL_FLAG_DONT_REMEMBER_BOOT_CHOICE
						JNE		@f
						CALL	LOAD_OPTION
@@:

						CALL	BOOTMENU_SETUP

BOOTMENU_OUTER_LOOP:
						CALL	MENU_WAIT_PAD_RELEASE

						MOV		AX,DISPLACED_DEMOSPLASh_MESSAGE_LOCATION
						CALL	DEMOSPLASH_MESSAGE_MOVABLE

						MOV		AX,DISPLACED_DEMOSPLASh_MESSAGE_LOCATION
						ADD		AL,5
						CALL	HIROSAKI_MESSAGE_MOVABLE

						MOV		AX,INSTRUCTION_LOCATION
						CALL	LOCATE
						MOV		SI,OFFSET INSTRUCTION_MESSAGE
						CALL	PRINT_TALL

						MOV		SI,OFFSET BOOTMENU_ITEM_BUFFER
						MOVZX	CX,CS:[NUM_BOOTMENU_OPTIONS]
						CALL	DRAWMENU

						MOV		CL,CS:[NUM_BOOTMENU_OPTIONS]
						MOV		AL,CS:[BOOTMENU_SELECTION]
						CALL	DRAWARROW


						; Wait for PAD while count down for AUTOBOOT >>
BOOTMENU_PAD_LOOP:
						CMP		BYTE PTR CS:[AUTOBOOT],0
						JE		BOOTMENU_PAD_READ
						MOV		DX,IO_VSYNC
						IN		AL,DX
						AND		AL,1
						MOV		AH,CS:[AUTOBOOT_PREV_VSYNC]
						MOV		CS:[AUTOBOOT_PREV_VSYNC],AL
						XOR		AH,1
						AND		AH,AL
						JE		BOOTMENU_PAD_READ

						DEC		WORD PTR CS:[AUTOBOOT_COUNTER]
						MOV		AL,0FH	; Virtually Press A button
						CMP		WORD PTR CS:[AUTOBOOT_COUNTER],0
						JE		BOOTMENU_PAD_IS_IN

						MOV		AX,CS:[AUTOBOOT_COUNTER]
						MOV		DL,60
						DIV		DL
						AND		AH,AH
						JNE		BOOTMENU_PAD_READ

						CALL	ITOX8
						XCHG	AH,AL
						MOV		WORD PTR CS:[AUTOBOOT_NUMBER],AX

						MOV		AL,0CH
						CALL	COLOR

						MOV		AX,AUTOBOOT_MESSAGE_LOCATION
						CALL	LOCATE
						MOV		SI,OFFSET AUTOBOOT_MSG
						CALL	PRINT_TALL

						MOV		AX,FIRST_MENU_ITEM_LOCATION
						SUB		AH,4
						ADD		AL,CS:[BOOTMENU_SELECTION]
						CALL	LOCATE
						MOV		SI,OFFSET AUTOBOOT_NUMBER
						CALL	PRINT_TALL

						MOV		AL,0FH
						CALL	COLOR

BOOTMENU_PAD_READ:
						CALL	READ_PADA
						CMP		AL,3FH
						JE		BOOTMENU_PAD_LOOP
						; Wait for PAD while count down for AUTOBOOT <<



BOOTMENU_PAD_IS_IN:
						MOV		BYTE PTR CS:[AUTOBOOT],0	; Once pad is moved, no more auto boot
						PUSH	AX
						MOV		AX,AUTOBOOT_MESSAGE_LOCATION
						XOR		AH,AH
						CALL	LOCATE
						CALL	ERASE_LINE_TALL
						POP		AX

						MOV		AH,CS:[BOOTMENU_SELECTION]
						MOV		CH,CS:[NUM_BOOTMENU_OPTIONS]
						CALL	MOVE_ARROW_BY_PAD
						MOV		CS:[BOOTMENU_SELECTION],AH

						AND		AL,30H
						CMP		AL,30H
						JE		BOOTMENU_OUTER_LOOP

						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],0
						JE		BACK_TO_MAIN_MENU

						TEST	CS:[CONTROL_FLAGS],CONTROL_FLAG_DONT_REMEMBER_BOOT_CHOICE
						JNE		@f
						CALL	SAVE_OPTION
@@:
						CALL	CLEAR_FIVE_BELOW
						CALL	TRYBOOT
						CALL	CLEAR_FIVE_BELOW

						JMP		BOOTMENU_OUTER_LOOP

BACK_TO_MAIN_MENU:
						RET

BOOTMENU				ENDP



; Input
;   DS:SI	Menu Top
;   CX		Number of Items
; Output
;   CX,SI   Destroyed
DRAWMENU				PROC
						MOV		AX,FIRST_MENU_ITEM_LOCATION
@@:
						PUSH	CX
						PUSH	AX
						PUSH	SI
						CALL	LOCATE
						CALL	PRINT_TALL
						POP		SI
						POP		AX
						POP		CX
						INC		AL
						ADD		SI,MENUITEM_LENGTH
						LOOP	@b

						RET
DRAWMENU				ENDP



; Input
;   DS:SI	Menu Top
;   CX		Number of Items
; Output
;   CX,SI   Destroyed
DRAWMENU2				PROC
						MOV		AX,FIRST_MENU_ITEM_LOCATION
DRAWMENU2_OUTER_LOOP:
						PUSH	CX
						PUSH	AX
						PUSH	SI
						CALL	LOCATE
						CALL	PRINT_TALL
						POP		SI

						CALL	SKIP_CSTR_DS_SI

						POP		AX
						POP		CX
						INC		AL
						LOOP	DRAWMENU2_OUTER_LOOP

						RET
DRAWMENU2				ENDP



; Input
;   DS:SI   C-String
; Output
;   DS:SI   Next C-String
SKIP_CSTR_DS_SI			PROC
						INC		SI
						CMP		BYTE PTR [SI],0
						JNE		SKIP_CSTR_DS_SI
						INC		SI
						RET
SKIP_CSTR_DS_SI			ENDP



BOOTMENU_SETUP			PROC

						MOV		CS:[NUM_BOOTMENU_OPTIONS],BOOTMENU_ITEMNUM_PARTITION_0

						TEST	CS:[CONTROL_FLAGS],CONTROL_FLAG_DONT_SCAN_PARTITION
						JNE		BOOTMENU_SETUP_DONE


						MOV		BX,CS:[INCOMING_BX]

						MOV		AH,5
						MOV		AL,BH
						AND		AL, 07H
						OR		AL,0B0H

						XOR		CX,CX
						MOV		DX,1	; LBA=1 Partition Table

						MOV		BX,1

						MOV		DI,OFFSET PARTITION_TABLE

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH

						MOV		SI,OFFSET PARTITION_TABLE+20H
						MOV		DI,OFFSET BOOTMENU_ITEM_BUFFER+80*BOOTMENU_ITEMNUM_PARTITION_0+17
						MOV		CX,5	; Up to 5 partitions

BOOTMENU_SETUP_OUTER_LOOP:
						CMP		BYTE PTR CS:[SI+1],0
						JE		BOOTMENU_SETUP_DONE
						CMP		BYTE PTR CS:[SI+1],90H	; I don't know about 90H, but HD Boot Loader stops checking when it see 90H
						JE		BOOTMENU_SETUP_DONE

						PUSH	SI
						PUSH	DI
						PUSH	CX
						PUSH	ES

						MOV		CX,CS
						MOV		ES,CX
						MOV		CX,16
						ADD		SI,20H
						REP		MOVSB
						MOV		AL,']'
						STOSB
						XOR		AL,AL
						STOSB

						POP		ES
						POP		CX
						POP		DI
						POP		SI

						INC		CS:[NUM_BOOTMENU_OPTIONS]
						ADD		SI,030H
						ADD		DI,MENUITEM_LENGTH
						LOOP	BOOTMENU_SETUP_OUTER_LOOP

BOOTMENU_SETUP_DONE:

						MOV		CL,CS:[BOOTMENU_SELECTION]
						CMP		CL,CS:[NUM_BOOTMENU_OPTIONS]
						JAE		@f
						MOV		CS:[BOOTMENU_SELECTION],CL

@@:
						RET

BOOTMENU_SETUP			ENDP



BOOTMENU_PROMPT_CD_CHANGE	PROC
						CALL	MENU_WAIT_PAD_RELEASE

						CALL	CLEAR_FIVE_BELOW
						MOV		AX,050AH
						MOV		SI,OFFSET CD_CHANGE_MESSAGE
						CALL	DRAW_TEXT
@@:
						CALL	READ_PADA
						CMP		AL,3FH
						JE		@b

						RET
BOOTMENU_PROMPT_CD_CHANGE	ENDP

CD_CHANGE_MESSAGE		DB		"INSERT TOWNS-APP CD IN THE SCSI CD DRIVE, AND",0
						DB		"PRESS A GAMEPAD0 BUTTON.",0
						DB		0
						DB		0
						DB		"NOTE: NOT ALL TOWNS-APPs CAN BE STARTED FROM",0
						DB		"      THE SCSI CD DRIVE.  SOME REQUIRE A PATCH.",0
						DB		0FFH,0FFH



LOAD_OPTION				PROC

						MOV		BX,CS:[INCOMING_BX]

						MOV		AH,5
						MOV		AL,BH
						AND		AL, 07H
						OR		AL,0B0H

						MOV		ECX,CS:[SELF_LBA]
						ADD		ECX,((OFFSET BOOTMENU_SELECTION)-(OFFSET MAIN))/512
						MOV		DX,CX	; DX=LBA Low
						SHR		ECX,16	; CX=LBA High
						XOR		CH,CH	; CH needs to be zero

						MOV		BX,1

						MOV		DI,OFFSET BOOTMENU_SELECTION

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH

						RET

LOAD_OPTION				ENDP



SAVE_OPTION				PROC

						MOV		BX,CS:[INCOMING_BX]

						MOV		AH,6	; Write sector (Question is if SYSROM supports this command)
						MOV		AL,BH
						AND		AL, 07H
						OR		AL,0B0H

						MOV		ECX,CS:[SELF_LBA]
						ADD		ECX,((OFFSET BOOTMENU_SELECTION)-(OFFSET MAIN))/512
						MOV		DX,CX	; DX=LBA Low
						SHR		ECX,16	; CX=LBA High
						XOR		CH,CH	; CH needs to be zero

						MOV		BX,1

						MOV		DI,OFFSET BOOTMENU_SELECTION

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH

						RET

SAVE_OPTION				ENDP



MENU_WAIT_PAD_RELEASE	PROC
						MOV		AX,INSTRUCTION_LOCATION
						CALL	LOCATE
						MOV		SI,OFFSET ASKRELEASE_MESSAGE
						CALL	PRINT_TALL

						CALL	WAIT_PADA_RELEASE

						MOV		AX,INSTRUCTION_LOCATION
						CALL	LOCATE
						CALL	ERASE_LINE_TALL

						RET
MENU_WAIT_PAD_RELEASE	ENDP



; Input
;   AL		Selection
;   CL		Number of menu items
DRAWARROW				PROC
						PUSH	AX

						MOV		AX,FIRST_MENU_ITEM_LOCATION
						SUB		AH,4
						AND		CH,CH

@@:
						PUSH	AX
						PUSH	CX
						CALL	LOCATE
						MOV		SI,OFFSET ARROW_ERASE
						CALL	PRINT_TALL
						POP		CX
						POP		AX
						INC		AL
						LOOP	@b

						MOV		AL,0CH
						CALL	COLOR

						POP		CX
						MOV		AX,FIRST_MENU_ITEM_LOCATION
						SUB		AH,4
						ADD		AL,CL
						CALL	LOCATE
						MOV		SI,OFFSET ARROW_DRAW
						CALL	PRINT_TALL

						MOV		AL,0FH
						CALL	COLOR

						RET

DRAWARROW				ENDP



TRYBOOT					PROC
						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_RETURN_TO_MAINMENU
						JNE		@f
						RET

@@:
						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_SCSI_CD
						JNE		TRYBOOT_NOT_FROM_SCSI_CD
						TEST	CS:[CONTROL_FLAGS],CONTROL_FLAG_PROMPT_CD_CHANGE
						JE		@f
						CALL	BOOTMENU_PROMPT_CD_CHANGE
@@:
						CALL	BOOT_FROM_SCSI_CD
						RET


TRYBOOT_NOT_FROM_SCSI_CD:
						XOR		DX,DX
						XOR		CX,CX

						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_INTERNAL_CD
						JNE		@f

						; Boot from Internal CD
						MOV		AL,0C0H
						MOV		BX,0008H
						JMP		TRYBOOT_FROMDEVICE

@@:
						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_SCSI_HDD_0
						JAE		@f

						; Boot from Floppy Disk
						MOV		AL,CS:[BOOTMENU_SELECTION]
						SUB		AL,BOOTMENU_ITEMNUM_FD0
						OR		AL,20H
						MOV		AH,3  ; Restore
						XOR		CH,CH
						DB		9AH		; CALLF FFFB:0014
						DW		0014H,0FFFBH


						MOV		BH,CS:[BOOTMENU_SELECTION]
						SUB		BH,BOOTMENU_ITEMNUM_FD0
						MOV		BL,02H

						MOV		AL,BH
						OR		AL,20H
						MOV		DL,1		; Floppy Disk Sector starts at 1.
						JMP		TRYBOOT_FROMDEVICE

@@:
						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_PARTITION_0
						JAE		@f

						; Boot from a SCSI HDD
						MOV		BH,CS:[BOOTMENU_SELECTION]
						SUB		BH,BOOTMENU_ITEMNUM_SCSI_HDD_0
						MOV		BL,1

						MOV		AL,BH
						OR		AL,0B0H
						JMP		TRYBOOT_FROMDEVICE

@@:
						; Boot from Hard-Disk Partition
						CALL	SETUP_PARTITION_TABLE_B100_SEGMENT

						MOV		AL,CS:[BOOTMENU_SELECTION]
						SUB		AL,BOOTMENU_ITEMNUM_PARTITION_0
						MOV		AH,30H
						MUL		AH
						ADD		AX,20H
						MOV		SI,AX

						MOV		DX,WORD PTR CS:[SI+PARTITION_TABLE+2]	; LBA Low
						MOV		CL,BYTE PTR CS:[SI+PARTITION_TABLE+4]	; LBA High
						XOR		CH,CH

						MOV		BX,CS:[INCOMING_BX]
						AND		BH,7

						MOV		AL,BH
						OR		AL,0B0H

						JMP		TRYBOOT_FROMDEVICE
TRYBOOT					ENDP



; To tell Fujitsu's boot sector which partition it is booting from,
; partition table needs to be loaded from B100:0000, and
; boot-partition flag needs to be set according to the selected
; partition.
SETUP_PARTITION_TABLE_B100_SEGMENT	PROC
						PUSH	DS
						PUSH	ES


						MOV		AX,CS
						MOV		DS,AX
						MOV		AX,0B100H
						MOV		ES,AX

						MOV		SI,OFFSET PARTITION_TABLE
						XOR		DI,DI
						MOV		CX,512
						REP MOVSB


						MOV		DI,0020H
						MOV		CX,10
						XOR		AL,AL
@@:
						MOV		BYTE PTR ES:[DI],0	; Tentatively clear boot-partition flag
						ADD		DI,0030H
						LOOP	@b


						MOV		AL,CS:[BOOTMENU_SELECTION]
						SUB		AL,BOOTMENU_ITEMNUM_PARTITION_0
						MOV		AH,30H
						MUL		AH
						ADD		AX,20H
						MOV		DI,AX
						MOV		BYTE PTR ES:[DI],0FFH	; Boot-partition flag

						POP		ES
						POP		DS
						RET
SETUP_PARTITION_TABLE_B100_SEGMENT	ENDP



; Input
;   AL   	Boot Device Code for BIOS
;	CL|DX	Boot Sector as Specified by BIOS
;	BX		Boot Device ID
; 		 BL:Device Type   1:HD/CD  2:FD  8:CD
TRYBOOT_FROMDEVICE		PROC

						PUSH	DS
						PUSH	BX

						MOV		BX,0B000H
						MOV		DS,BX

						MOV		DWORD PTR DS:[0],0

						MOV		AH,05H	; Read Sector
						MOV		CH,0
						MOV		BX,4	; 4 sectors
						MOV		DI,0	; Offset=0

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH

						MOV		EAX,DS:[0]
						POP		BX
						POP		DS
						JB		TRYBOOT_READERROR

						CMP		EAX,344C5049H	; "IPL4"
						JNE		TRYBOOT_NOT_IPL4

						MOV		AX,0FFFFH

						; Uncomment following DB and DWs and comment CALL SETUP_INTERCEPT_AND_GO
						; for simple boot without patching IPL.
						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						;DB		9AH		; CALLF B000:0004
						;DW		 0004H
						;DW		0B000H
						CALL	SETUP_INTERCEPT_AND_GO

						PUSH	CS
						POP		DS

						RET

TRYBOOT_READERROR:
						MOV		AX,ERRORMESSAGE_LOCATION
						CALL	LOCATE
						MOV		SI,OFFSET READERROR_MESSAGE
						CALL	PRINT_TALL
						RET

TRYBOOT_NOT_IPL4:
						MOV		AX,ERRORMESSAGE_LOCATION
						CALL	LOCATE
						MOV		SI,OFFSET NOT_IPL4_MESSAGE
						CALL	PRINT_TALL
						RET


TRYBOOT_FROMDEVICE		ENDP



INSTRUCTION_MESSAGE		DB		"USE GAMEPAD0 UP/DOWN TO MOVE CURSOR, A-BUTTON TO SELECT BOOT DEVICE",0
ASKRELEASE_MESSAGE		DB		"PLEASE RELEASE PAD0 BUTTONS                                        ",0

READERROR_MESSAGE		DB		"FAILED TO READ BOOT SECTOR",0
NOT_IPL4_MESSAGE		DB		"NOT A BOOTABLE MEDIA      ",0

NUM_BOOTMENU_OPTIONS	DB		15

BOOTMENU_ITEM_BUFFER	DB		"BACK TO MAIN MENU",0,62 dup(0)
						DB		"INTERNAL CD DRIVE",0,62 dup(0)
						DB		"FLOPPY DRIVE A   ",0,62 dup(0)
						DB		"FLOPPY DRIVE B   ",0,62 dup(0)
						DB		"SCSI CD DRIVE    ",0,62 dup(0)
						DB		"SCSI HDD ID=0    ",0,62 dup(0)
						DB		"SCSI HDD ID=1    ",0,62 dup(0)
						DB		"SCSI HDD ID=2    ",0,62 dup(0)
						DB		"SCSI HDD ID=3    ",0,62 dup(0)
						DB		"SCSI HDD ID=4    ",0,62 dup(0)
						DB		"HDD PARTITION 0 [",0,62 dup(0)
						DB		"HDD PARTITION 1 [",0,62 dup(0)
						DB		"HDD PARTITION 2 [",0,62 dup(0)
						DB		"HDD PARTITION 3 [",0,62 dup(0)
						DB		"HDD PARTITION 4 [",0,62 dup(0)
						; DB		"HDD PARTITION 5 [",0,62 dup(0)
						; DB		"HDD PARTITION 6 [",0,62 dup(0)
						; DB		"HDD PARTITION 7 [",0,62 dup(0)
						; DB		"HDD PARTITION 8 [",0,62 dup(0)
						; DB		"HDD PARTITION 9 [",0,62 dup(0)

ARROW_ERASE				DB		"   ",0
ARROW_DRAW				DB		">>>",0

AUTOBOOT				DB		1
AUTOBOOT_PREV_VSYNC		DB		0
AUTOBOOT_COUNTER		DW		721		; 12*VSYNC_PER_SEC+1

AUTOBOOT_MSG			DB		"AUTO BOOT IN "
AUTOBOOT_NUMBER			DB		"00",0
