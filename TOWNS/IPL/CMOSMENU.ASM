
CMOSMENU_NUM_ITEMS_ICM	EQU	7
CMOSMENU_NUM_ITEMS_OTHER	EQU	4

CMOSMENU_JUMP_TABLE		DW	OFFSET CMOSMENU_BACK_TO_MAINMENU
						DW	OFFSET CMOSMENU_DRIVE_LETTER
						DW	OFFSET CMOSMENU_RESET_CMOS
						DW	OFFSET CMOSMENU_FASTMODE
						DW	OFFSET CMOSMENU_AUTO_RESTORE
						DW	OFFSET CMOSMENU_BACKUP
						DW	OFFSET CMOSMENU_RESTORE


CMOSMENU_ITEMS			DB	"BACK TO MAIN MENU",0
						DB	"DRIVE LETTER ASSIGNMENT",0
						DB	"RESET CMOS",0
CMOSMENU_FASTMODE_MENU	DB	"[ ] FAST MODE",0
CMOSMENU_AUTO_RESTORE_MENU	DB	"[ ] AUTO CMOS RESTORE FROM MEMORY CARD",0
						DB  "BACK UP CMOS TO MEMORY CARD NOW",0
						DB	"RESTORE CMOS FROM MEMORY CARD NOW",0
CMOSMENU_ITEMS_END:

CMOSMENU_NUM_ITEMS		DB	CMOSMENU_NUM_ITEMS_OTHER
CMOSMENU_SELECTION		DB	0


CMOSMENU_ERR_CMOS_NOT_BACKEDUP	DB	"CMOS HAS NOT BEEN BACKED UP.",0,0FFH,0FFH
CMOSMENU_CMOS_BACKEDUP	DB	"BACKED UP CMOS.",0,0FFH,0FFH
CMOSMENU_CMOS_RESTORED	DB	"RESTORED CMOS.",0,0FFH,0FFH



CMOSMENU				PROC
						MOV		BYTE PTR CS:[CMOSMENU_SELECTION],0

						MOV		BX,CS:[INCOMING_BX]
						AND		BX,0F0FH

						MOV		AL,CMOSMENU_NUM_ITEMS_ICM
						CMP		BL,4		; ICM?
						JE		SHORT CMOSMENU_BOOT_FROM_ICM
						MOV		AL,CMOSMENU_NUM_ITEMS_OTHER

CMOSMENU_BOOT_FROM_ICM:
						MOV		CS:[CMOSMENU_NUM_ITEMS],AL


CMOSMENU_OUTERLOOP:
						CALL	MENU_WAIT_PAD_RELEASE
						CALL	CLEAR_FIVE_BELOW
						CALL	CMOSMENU_UPDATE_MENU

						MOVZX	CX,CS:[CMOSMENU_NUM_ITEMS]
						MOV		SI,OFFSET CMOSMENU_ITEMS
						CALL	DRAWMENU2

CMOSMENU_INNER_LOOP:
						MOVZX	CX,CS:[CMOSMENU_NUM_ITEMS]
						MOV		AL,CS:[CMOSMENU_SELECTION]
						CALL	DRAWARROW

CMOSMENU_WAIT_PAD:
						CALL	READ_PADA
						CMP		AL,3FH
						JE		CMOSMENU_WAIT_PAD

						; Input
						;   AL Pad
						;   AH Current Selection
						;   CH Number of Choices
						; Output
						;   AH New Selection
						MOV		AH,CS:[CMOSMENU_SELECTION]
						MOV		CH,CS:[CMOSMENU_NUM_ITEMS]
						CALL	MOVE_ARROW_BY_PAD
						MOV		CS:[CMOSMENU_SELECTION],AH

						AND		AL,30H
						CMP		AL,30H
						JE		CMOSMENU_INNER_LOOP

						MOVZX	BX,CS:[CMOSMENU_SELECTION]
						SHL		BX,1
						JMP		CS:[CMOSMENU_JUMP_TABLE+BX]



CMOSMENU_DRIVE_LETTER:
						CALL	DRVMENU
						JMP		CMOSMENU_OUTERLOOP



CMOSMENU_RESET_CMOS:
						CALL	CMOSRESET_CONFIRM
						JMP		CMOSMENU_OUTERLOOP



CMOSMENU_FASTMODE:
						JMP		CMOSMENU_OUTERLOOP



CMOSMENU_AUTO_RESTORE:
						MOV		AL,1
						MOV		DI,OFFSET AUTO_CMOS_RESTORE
						SUB		AL,CS:[DI]
						CALL	ICM_WRITE_SETTING_AL
						JMP		CMOSMENU_OUTERLOOP



CMOSMENU_BACKUP:
						; Input
						;	AL     Which segment selector:
						;			bit0:DS
						;			bit1:ES
						;			bit2:FS
						;			bit3:GS
						;   EDX    Physical Address
						MOV		AL,2	; Bit1=ES
						MOV		EDX,ICM_CMOS_BACKUP_PHYSADDR
						CALL	GET_UNREALMODE_SEG
						MOV		DX,3000h
						XOR		DI,DI
CMOSMENU_BACKUP_OUTER_LOOP:
						MOV		CX,4
CMOSMENU_BACKUP_INNER_LOOP:
						IN		AL,DX
						ROR		EAX,8
						INC		DX
						INC		DX
						LOOP	CMOSMENU_BACKUP_INNER_LOOP
						STOSD
						CMP		DI,4000h
						JB		CMOSMENU_BACKUP_OUTER_LOOP

						PUSH	CS
						POP		ES

						MOV		DI,OFFSET CMOS_BACKED_UP
						MOV		AL,1
						CALL	ICM_WRITE_SETTING_AL

						MOV		SI,OFFSET CMOSMENU_CMOS_BACKEDUP
						CALL	SHOW_MESSAGE_WAIT

						JMP		CMOSMENU_OUTERLOOP



CMOSMENU_RESTORE:
						CMP		BYTE PTR CS:[CMOS_BACKED_UP],0
						JNE		CMOSMENU_RESTORE_GO

						MOV		SI,OFFSET CMOSMENU_ERR_CMOS_NOT_BACKEDUP
						CALL	SHOW_MESSAGE_WAIT
						JMP		CMOSMENU_OUTERLOOP

CMOSMENU_RESTORE_GO:
						MOV		AL,1	; Bit0=DS
						MOV		EDX,ICM_CMOS_BACKUP_PHYSADDR
						CALL	GET_UNREALMODE_SEG
						MOV		DX,3000h
						XOR		SI,SI
CMOSMENU_RESTORE_OUTER_LOOP:
						MOV		CX,4
						LODSD
CMOSMENU_RESTORE_INNER_LOOP:
						OUT		DX,AL
						ROR		EAX,8
						INC		DX
						INC		DX
						LOOP	CMOSMENU_RESTORE_INNER_LOOP
						CMP		SI,4000h
						JB		CMOSMENU_RESTORE_OUTER_LOOP

						PUSH	CS
						POP		DS

						MOV		SI,OFFSET CMOSMENU_CMOS_RESTORED
						CALL	SHOW_MESSAGE_WAIT

						JMP		CMOSMENU_OUTERLOOP



CMOSMENU_BACK_TO_MAINMENU:
						RET

CMOSMENU				ENDP



CMOSMENU_UPDATE_MENU	PROC
						MOV		AL,' '
						CMP		BYTE PTR CS:[AUTO_CMOS_RESTORE],0
						JE		CMOSMENU_UPDATE_AUTO_RESTORE
						MOV		AL,'X'
CMOSMENU_UPDATE_AUTO_RESTORE:
						MOV		CS:[CMOSMENU_AUTO_RESTORE_MENU+1],AL

						RET
CMOSMENU_UPDATE_MENU	ENDP



; DI	OFFSET
; AL	Value
; Needs to be WORD access.
; To be safe, DWORD access, which has been confirmed to work.
; Writes to both FRAM and the Config Area
ICM_WRITE_SETTING_AL	PROC
						PUSH	FS
						PUSH	DI
						PUSH	CX
						PUSH	EAX
						PUSH	EDX

						MOV		CS:[DI],AL	; Write to own config area.

						MOV		CL,AL

						MOV		AL,4	; Bit2=FS
						MOV		EDX,ICM_LOADER_TOP
						CALL	GET_UNREALMODE_SEG

						MOV		BX,DI
						AND		BX,3
						AND		DI,0FFFCH

						MOV		EAX,FS:[DI]
						PUSH	EAX
						ADD		BX,SP
						MOV		SS:[BX],CL
						POP		EAX

						MOV		FS:[DI],EAX

						POP		EDX
						POP		EAX
						POP		CX
						POP		DI
						POP		FS
						RET
ICM_WRITE_SETTING_AL	ENDP



; SI Text Message Pointer
SHOW_MESSAGE_WAIT		PROC
						PUSH	SI
						CALL	MENU_WAIT_PAD_RELEASE
						CALL	CLEAR_FIVE_BELOW
						POP		SI

						MOV		AX,050AH
						CALL	DRAW_TEXT
@@:
						CALL	READ_PADA
						CMP		AL,3FH
						JE		@b

						RET
SHOW_MESSAGE_WAIT		ENDP
