						ASSUME	CS:CODE,DS:CODE


VERSION_STRING			EQU		"VERSION 20201003"

						.386p
CODE					SEGMENT	USE16

						INCLUDE		IODEF.ASM
						INCLUDE		DEF.ASM

MAIN					PROC
						; ****ing MASM kindly inserts NOP after JMP and messes up the offsets.  Good job, Microsoft.
						DB		0EBH,(REALMAIN-@f)  ; JMP		REALMAIN
@@:
						DB		"RESCUE"
												; Offsets must be consisntent with DEF.H
CONTROL_FLAGS			DW		0				; +8
YSSCSICD_LBA			DD		0				; +10  For CD and HD IPL
YSSCSICD_SECTOR_COUNT	DB		24				; +14  For CD and HD IPL
						DB		0				; +15  Not used at this time.
SELF_LBA				DD		0				; +16  Loader LBA (Set only when booting from HD)

INCOMING_BX				DW		0
LOADER_BIOS_DEVICE_ID	DB		0
YSSCSICD_LOADED			DB		0

REALMAIN:
; Incoming BL:Device Type   1:HD/CD  2:FD  8:CD
; Incoming BH:Unit Number
						CLI
						PUSH	CS
						POP		DS

						MOV		BP,CS
						SUB		BP,1000H
						MOV		SS,BP
						MOV		SP,0FFFEH ; IO.SYS will reset the stack pointer anyway.

						MOV		CS:[INCOMING_BX],BX
						AND		BH,0FH
						AND		BL,0FH
						CMP		BL,1
						JNE		@f
						OR		BH,0B0H
						JMP		DEVICE_ID_SET
@@:
						CMP		BL,2
						JNE		@f
						OR		BH,020H
						JMP		DEVICE_ID_SET

@@:
						XOR		BX,BX

DEVICE_ID_SET:
						MOV		CS:[LOADER_BIOS_DEVICE_ID],BH


						CALL	INITGRAPHICS
						CALL	VRAMMODE
						CALL	CLS

						MOV		AL,15
						CALL	COLOR

				; CALL	TSUGARU_DEBUG_BREAK

						CALL	DRAW_AOMORI
						CALL	DEMOSPLASH_MESSAGE

						MOV		AX,0101H
						CALL	LOCATE
						MOV		SI,OFFSET MESSAGE_LINE0
						CALL	PRINT_TALL

						MOV		AX,0102H
						CALL	LOCATE
						MOV		SI,OFFSET MESSAGE_LINE1
						CALL	PRINT_TALL

						MOV		AX,0103H
						CALL	LOCATE
						MOV		SI,OFFSET MESSAGE_LINE2
						CALL	PRINT_TALL

						CALL	LOAD_YSSCSICD

						TEST	WORD PTR [CONTROL_FLAGS],CONTROL_FLAG_BOOT_MENU
						JNE		MAINMENU

						CALL	BOOT_FROM_SCSI_CD

						JMP		MAINMENU


MAIN					ENDP

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



DEMOSPLASH_MESSAGE		PROC
						MOV		AX,280AH
						MOV		SI,OFFSET MSG_DEMOSPLASH
						CALL	DRAW_TEXT
						RET
DEMOSPLASH_MESSAGE		ENDP

DEMOSPLASH_MESSAGE_MOVABLE		PROC
						MOV		SI,OFFSET MSG_DEMOSPLASH
						CALL	DRAW_TEXT
						RET
DEMOSPLASH_MESSAGE_MOVABLE		ENDP


MSG_DEMOSPLASH			DB		"PLEASE JOIN US AT DEMOSPLASH 2020",0
						DB		"AND 2021, 2022, ...",0
						DB		"http://demosplash.org",0
						DB		0FFH,0FFH



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



HIROSAKI_MESSAGE_MOVABLE		PROC
						MOV		SI,OFFSET MSG_HIROSAKI
						CALL	DRAW_TEXT
						RET
HIROSAKI_MESSAGE_MOVABLE		ENDP

MSG_HIROSAKI			DB		"VISIT HIROSAKI CITY",0
						DB		"IN AOMORI PREFECTURE, JAPAN",0
						DB		"AKA 'TSUGARU' REGION",0
						DB		"WHEN THIS PANDEMIC IS OVER.",0
						DB		"BEAUTIFUL CHERRY BLOSSOMS IN APRIL",0
						DB		"FAMOUS NEPUTA FESTIVAL IN AUGUST",0
						DB		"GORGEOUS IWAKI MOUNTAIN",0
						DB		"CRISPY JAPAN'S BEST APPLES",0
						DB		"CUTE HIROSAKI CASTLE",0
						DB		0FFH,0FFH



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



						INCLUDE		UTIL.ASM
						INCLUDE		DRAWFONT.ASM
						INCLUDE		MEMBANK.ASM
						INCLUDE		../SCSILIB/SCSIIO.ASM
						INCLUDE		SCSIUTIL.ASM
						INCLUDE		CDBOOT.ASM
						INCLUDE		LOADYSSC.ASM
						INCLUDE		GRAPHICS.ASM
						INCLUDE		../RESOURCE/AOMORI.ASM
						INCLUDE		TSUGARU.ASM
						INCLUDE		UI.ASM
						INCLUDE		HID_IO.ASM

MESSAGE_LINE0			DB	"FM TOWNS RESCUE BOOT LOADER BY CAPTAINYS",0
MESSAGE_LINE1			DB	VERSION_STRING,0
MESSAGE_LINE2			DB	"http://www.ysflight.com",0

						INCLUDE		MAINMENU.ASM
						INCLUDE		CMOSMENU.ASM
						INCLUDE		CMOSRST.ASM
						INCLUDE		BOOTMENU.ASM

CODE					ENDS

						END		MAIN
