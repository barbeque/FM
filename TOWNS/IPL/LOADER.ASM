						ASSUME	CS:CODE,DS:CODE


VERSION_STRING			EQU		"VERSION 20200930"

						.386p
CODE					SEGMENT	USE16

						INCLUDE		IODEF.ASM
						INCLUDE		DEF.ASM

MAIN					PROC
						; ****ing MASM kindly inserts NOP after JMP and messes up the offsets.  Good job, Microsoft.
						DB		0EBH,(REALMAIN-@f)  ; JMP		REALMAIN
@@:
						DB		"RESCUE"
												; Offsets must be consisntent with DEF.H
CONTROL_FLAGS			DW		0				; +8
YSSCSICD_LBA			DD		0				; +10  For CD and HD IPL
YSSCSICD_SECTOR_COUNT	DB		24				; +14  For CD and HD IPL
						DB		0				; +15  Not used at this time.
SELF_LBA				DD		0				; +16  Loader LBA (Set only when booting from HD)

INCOMING_BX				DW		0
LOADER_BIOS_DEVICE_ID	DB		0
YSSCSICD_LOADED			DB		0

REALMAIN:
; Incoming BL:Device Type   1:HD/CD  2:FD  8:CD
; Incoming BH:Unit Number
						CLI
						PUSH	CS
						POP		DS

						MOV		BP,CS
						SUB		BP,1000H
						MOV		SS,BP
						MOV		SP,0FFFEH ; IO.SYS will reset the stack pointer anyway.

						MOV		CS:[INCOMING_BX],BX
						AND		BH,0FH
						AND		BL,0FH
						CMP		BL,1
						JNE		@f
						OR		BH,0B0H
						JMP		DEVICE_ID_SET
@@:
						CMP		BL,2
						JNE		@f
						OR		BH,020H
						JMP		DEVICE_ID_SET

@@:
						XOR		BX,BX

DEVICE_ID_SET:
						MOV		CS:[LOADER_BIOS_DEVICE_ID],BH


						CALL	INITGRAPHICS
						CALL	VRAMMODE
						CALL	CLS

						MOV		AL,15
						CALL	COLOR

				; CALL	TSUGARU_DEBUG_BREAK

						CALL	DRAW_AOMORI
						CALL	DEMOSPLASH_MESSAGE

						MOV		AX,0101H
						CALL	LOCATE
						MOV		SI,OFFSET MESSAGE_LINE0
						CALL	PRINT_TALL

						MOV		AX,0102H
						CALL	LOCATE
						MOV		SI,OFFSET MESSAGE_LINE1
						CALL	PRINT_TALL

						MOV		AX,0103H
						CALL	LOCATE
						MOV		SI,OFFSET MESSAGE_LINE2
						CALL	PRINT_TALL

						CALL	LOAD_YSSCSICD

						TEST	WORD PTR [CONTROL_FLAGS],CONTROL_FLAG_PROMPT_CD
						JNE		FROM_SCSI_IPL

						TEST	WORD PTR [CONTROL_FLAGS],CONTROL_FLAG_BOOT_MENU
						JNE		BOOTMENU

						CALL	BOOT_FROM_SCSI_CD
@@:
						JMP			@b


FROM_SCSI_IPL:
			; CALL	TSUGARU_DEBUG_BREAK
@@:
						CALL	PROMPT_CD_CHANGE
						CALL	BOOT_FROM_SCSI_CD
						JMP		@b


MAIN					ENDP

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



DEMOSPLASH_MESSAGE		PROC
						MOV		AX,280AH
						JMP		DEMOSPLASH_MESSAGE_MOVABLE
DEMOSPLASH_MESSAGE		ENDP

DEMOSPLASH_MESSAGE_MOVABLE		PROC
						PUSH	AX
						CALL	LOCATE
						MOV		SI,OFFSET MSG_DEMOSPLASH_1
						CALL	PRINT_TALL
						POP		AX
						INC		AL

						PUSH	AX
						CALL	LOCATE
						MOV		SI,OFFSET MSG_DEMOSPLASH_2
						CALL	PRINT_TALL
						POP		AX
						INC		AL

						CALL	LOCATE
						MOV		SI,OFFSET MSG_DEMOSPLASH_3
						CALL	PRINT_TALL

						RET
DEMOSPLASH_MESSAGE_MOVABLE		ENDP


MSG_DEMOSPLASH_1		DB		"PLEASE JOIN US AT DEMOSPLASH 2020",0
MSG_DEMOSPLASH_2		DB		"AND 2021, 2022, ...",0
MSG_DEMOSPLASH_3		DB		"http://demosplash.org",0



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



						INCLUDE		UTIL.ASM
						INCLUDE		DRAWFONT.ASM
						INCLUDE		MEMBANK.ASM
						INCLUDE		../SCSILIB/SCSIIO.ASM
						INCLUDE		SCSIUTIL.ASM
						INCLUDE		CDBOOT.ASM
						INCLUDE		LOADYSSC.ASM
						INCLUDE		GRAPHICS.ASM
						INCLUDE		../RESOURCE/AOMORI.ASM
						INCLUDE		TSUGARU.ASM
						INCLUDE		UI.ASM
						INCLUDE		HID_IO.ASM

MESSAGE_LINE0			DB	"FM TOWNS RESCUE BOOT LOADER BY CAPTAINYS",0
MESSAGE_LINE1			DB	VERSION_STRING,0
MESSAGE_LINE2			DB	"http://www.ysflight.com",0

						INCLUDE		MENU.ASM

CODE					ENDS

						END		MAIN
