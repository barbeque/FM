; Incoming BL:Device Type   1:HD/CD  2:FD  4:ICM  8:CD
; Incoming BH:Unit Number

LOAD_YSSCSICD			PROC
						MOV		DX,0404H ; TOWNSIO_FMR_VRAM_OR_MAINRAM
						IN		AL,DX
						PUSH	AX
						MOV		AL,80H
						OUT		DX,AL

						MOV		WORD PTR [LOAD_YSSCSICD_BUFFADDR],0
						MOV		BX,CS:[INCOMING_BX]
						CMP		BL,2
						JE		LOAD_YSSCSICD_FD
						CMP		BL,1
						JE		LOAD_YSSCSICD_FROM_SCSI
						; CMP		BL,1
						; JNE		YSSCSICD_LOAD_DONE
						; JMP		LOAD_YSSCSICD_HD
						CMP		BL,4
						JE		LOAD_YSSCSICD_FROM_ICM

YSSCSICD_LOAD_DONE:
						POP		AX
						MOV		DX,0404H ; TOWNSIO_FMR_VRAM_OR_MAINRAM
						OUT		DX,AL
						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

LOAD_YSSCSICD_FD:
						PUSH	DS

						MOV		AX,YSSCSICD_FD_BEGIN_TRACK*2+YSSCSICD_FD_BEGIN_SIDE
						MOV		CS:[LOAD_YSSCSICD_TRK_SIDE],AX

						MOV		AX,YSSCSICD_SEGMENT
						MOV		DS,AX

@@:
						MOV		AH,05H	 ; Read Sector
						MOV		AL,CS:[LOADER_BIOS_DEVICE_ID]
						MOV		CX,CS:[LOAD_YSSCSICD_TRK_SIDE]
						XOR		DH,DH
						SHR		CX,1
						RCL		DH,1
						MOV		DL,1
						MOV		BX,8
						MOV		DI,CS:[LOAD_YSSCSICD_BUFFADDR]

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH

						JB		SHORT @f

						INC		WORD PTR CS:[LOAD_YSSCSICD_TRK_SIDE]
						ADD		WORD PTR CS:[LOAD_YSSCSICD_BUFFADDR],8192
						MOV		DI,CS:[LOAD_YSSCSICD_BUFFADDR]
						CMP		DI,0C000H
						JB		@b

						MOV		BYTE PTR CS:[YSSCSICD_LOADED],1

@@:
						POP		DS
						JMP		YSSCSICD_LOAD_DONE


LOAD_YSSCSICD_FROM_SCSI:
						PUSH	DS

						MOV		SI,OFFSET SCSI_READ_SECTOR_CMD

						MOV		EDX,CS:[YSSCSICD_LBA]
						MOV		[SI+4],DH
						MOV		[SI+5],DL
						ROR		EDX,16
						MOV		[SI+2],DH
						MOV		[SI+3],DL

						MOV		DL,CS:[YSSCSICD_SECTOR_COUNT]
						MOV		[SI+8],DL

						MOV		EDI,YSSCSICD_SEGMENT*16

						MOV		CL,BYTE PTR CS:[INCOMING_BX+1]	; Incoming BH=Unit Number

						CALL	SCSI_COMMAND
						JB		@b

						MOV		BYTE PTR CS:[YSSCSICD_LOADED],1

@@:
						POP		DS
						JMP		YSSCSICD_LOAD_DONE


LOAD_YSSCSICD_FROM_ICM:
						PUSH	DS

						MOV		AX,YSSCSICD_SEGMENT
						MOV		DS,AX
						MOV		DI,CS:[LOAD_YSSCSICD_BUFFADDR]

						MOV		AH,05H	 ; Read Sector
						MOV		AL,CS:[LOADER_BIOS_DEVICE_ID]

						MOV		CX,WORD PTR CS:[YSSCSICD_LBA+2]
						MOV		DX,WORD PTR CS:[YSSCSICD_LBA]
						XOR		CH,CH	; Just in case

						MOV		BX,8	; 8 sectors

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH

						JB		SHORT @f

						MOV		BYTE PTR CS:[YSSCSICD_LOADED],1
@@:
						POP		DS
						JMP		YSSCSICD_LOAD_DONE


LOAD_YSSCSICD			ENDP





LOAD_YSSCSICD_TRK_SIDE	DW	0
LOAD_YSSCSICD_BUFFADDR	DW	0
